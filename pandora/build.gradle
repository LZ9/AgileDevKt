plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace 'com.lodz.android.pandora'
    compileSdkVersion 36
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        versionCode 71
        versionName "2.1.3"// 成功上传
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
//    api 'ink.lodz:core-kt:2.1.2'
    api project(':corekt')

    // RxJava
    api 'io.reactivex.rxjava3:rxjava:3.1.12'
    // Retrofit
    api 'com.squareup.retrofit2:retrofit:3.0.0'
    api 'com.squareup.retrofit2:adapter-rxjava3:3.0.0'
    api 'com.squareup.retrofit2:converter-jackson:3.0.0'
    api 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    // RxJava生命周期
    api 'com.trello.rxlifecycle4:rxlifecycle-components:4.0.2'
    // EventBus
    api 'org.greenrobot:eventbus:3.3.1'
}


//----------------------- 发布到 Maven Central  ------------------------------
String PUBLISH_GROUP_ID = 'ink.lodz'
String PUBLISH_ARTIFACT_ID = 'pandora'
String PUBLISH_VERSION = '2.1.3'

mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()

    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)

    pom {

        name = PUBLISH_ARTIFACT_ID
        description = "This is a android base component library."
        inceptionYear = "2025"
        url = 'https://github.com/LZ9/AgileDevKt'
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "Lodz"
                name = "Lodz"
                email = "lz.mms@foxmail.com"
                url = "https://github.com/LZ9"
            }
        }
        scm {
            url = 'https://github.com/LZ9/AgileDevKt/tree/master'
            connection = 'scm:git:github.com/LZ9/AgileDevKt.git'
            developerConnection = 'scm:git:ssh://github.com/LZ9/AgileDevKt.git'
        }
    }
}