plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.lodz.android.agiledevkt'
    compileSdkVersion 36
    defaultConfig {
        applicationId "com.lodz.android.agiledevkt"
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true// 启用dex分包
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
//        ndk {
//            abiFilters "armeabi"
//        }
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        buildConfig = true
    }

    def LOG_DEBUG = "LOG_DEBUG"
    def FILE_AUTHORITY = "FILE_AUTHORITY"
    defaultConfig {
        buildConfigField "boolean", LOG_DEBUG, "true"
        buildConfigField "String", FILE_AUTHORITY, "\"com.lodz.android.agiledevkt.fileprovider\""
    }

    buildTypes {

        debug {//debug版本的配置
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled false // 是否支持zip
            debuggable true // 是否支持调试
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [FILE_AUTHORITY: defaultConfig.getBuildConfigFields().get("FILE_AUTHORITY").value.replace("\"", "")]
        }

        release {
            buildConfigField "boolean", LOG_DEBUG, "false"
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled true // 是否支持zip
            debuggable false // 是否支持调试
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [FILE_AUTHORITY: defaultConfig.getBuildConfigFields().get("FILE_AUTHORITY").value.replace("\"", "")]
        }
    }

    // 程序在编译的时候会检查lint，有任何错误提示会停止build，我们可以关闭这个开关
    lint {
        abortOnError false
        checkReleaseBuilds false
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName
            if (variant.baseName.endsWith('release')) {
                def formattedDate = new Date().format('yyyyMMddHHmm')
                newName = "AgileDevKt-v" + defaultConfig.versionName + "-" + variant.baseName + "-" + formattedDate + ".apk"
            } else {
                newName = "AgileDevKt-v" + defaultConfig.versionName + "-" + variant.baseName + ".apk"
            }
            outputFileName = newName
        }
    }
}

dependencies {
    implementation project(':pandora')
    implementation project(':imageloaderkt')

    // 权限申请
    implementation "com.github.permissions-dispatcher:ktx:1.1.4"

    // 腾讯定位SDK
    implementation files('libs/TencentLocationSdk_v7.1.4.3_r4aa8ac63_20180911_165428.jar')
    // 高德定位SDK
    implementation 'com.amap.api:location:4.7.0'

    // 大图加载缩放
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    // 滚动选择
//    implementation 'com.contrarywind:Android-PickerView:4.1.9'
    implementation files('libs/Android-PickerView-4.1.9.aar')
    implementation files('libs/wheelview-4.1.0.aar')

    //添加RxDownload4的所有依赖
    implementation "com.github.ssseasonnn:RxDownload:1.1.4"

    // okio
    implementation 'com.squareup.okio:okio:3.1.0'

    // 单元测试
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // jsbridge
    implementation 'ink.lodz:jsbridge-kt:1.0.5'

    implementation "com.airbnb.android:lottie:5.2.0"

}